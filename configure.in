dnl Process this file with autoconf to produce a configure script.
AC_INIT(uuid/uuid.c)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(freedce, 1.1.0.5)

AC_PREFIX_DEFAULT(/opt/dce)

dnl Checks for programs.
AM_PROG_LIBTOOL
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_LN_S
AM_PROG_LEX
AC_PROG_YACC
dnl Checks for libraries.
AC_CHECK_LIB(pthread, pthread_create)
RPC_CHECK_LIBDIR(pthd4exc_create, dcethreads, /opt/dce/lib,,
AC_MSG_ERROR([freedce requires libdcethreads!]))

AC_CHECK_HEADER(dce/pthread_exc.h,,[
rpc_save_cpp="$CPPFLAGS"
CPPFLAGS="-I/opt/dce/include $CPPFLAGS"
AC_CHECK_HEADER(/opt/dce/include/dce/pthread_exc.h,
CPPFLAGS="$rpc_save_cpp"
DCETHREADINCLUDES="-I/opt/dce/include"
,
AC_MSG_ERROR([cannot find dcethreads header files])
)
])
AC_SUBST(DCETHREADINCLUDES)

dnl Checks for header files.
dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP

AC_DEFINE(_REENTRANT)

dnl _GNU_SOURCE pulls in the definition of _pthread_push_defer
AC_DEFINE(_GNU_SOURCE)

AC_SUBST(target_cpu)
AC_SUBST(target_os)

dnl This allows us to re-use AC_OUTPUT which makes things more maintainable
dnl and helps us to use automake nicely.
dnl it is defined here and not in acinclude.m4 because acgeneral.m4 gets
dnl parsed AFTER acinclude.m4
pushdef([AC_OUTPUT], 
[define([RPC_OUTPUT_FILES], ifdef([RPC_OUTPUT_FILES], RPC_OUTPUT_FILES ,)[$1])]
)

RPC_ARG_DEFINE(debug, DEBUG, yes,Compile with debugging support)

RPC_ARG_DEFINE(dcom, ENABLE_DCOM, no,Enable experimental DCOM support)
AM_CONDITIONAL(ENABLE_DCOM, test x$rpc_arg_dcom = yes)

# FIXME: what exactly is AT386 anyway?
AM_CONDITIONAL(TARGET_OS_AT386, false)
AM_CONDITIONAL(TARGET_OS_LINUX, test x$target_os = xlinux-gnu)

dnl ============= include subdirs here
sinclude(uuid/config.m4)
sinclude(include/config.m4)
sinclude(idl/config.m4)
sinclude(idllib/config.m4)
sinclude(ncklib/config.m4)
sinclude(dcelib/config.m4)
sinclude(rpcd/config.m4)
sinclude(demos/config.m4)
sinclude(dcom/config.m4)

AC_OUTPUT(Makefile)

dnl restore original AC_OUTPUT definition
popdef([AC_OUTPUT])

dnl call AC_OUTPUT with the list of files we accumulated.
dnl automake will complain about RPC_OUTPUT_FILES.in, so
dnl we include a dummy file to keep it quiet.
AC_OUTPUT(RPC_OUTPUT_FILES)

